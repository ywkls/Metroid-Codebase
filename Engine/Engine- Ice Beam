ffc script Ice_Beam{
	void run(int Weapon_ID){
		this->Data = GH_INVISIBLE_COMBO;
		npc current_enemy;
		bool Stop = false;
		bool impact = false;//Tests whether an enemy or freezable combo has been hit.
		//Two variables used to save location of ffc upon impact.
		int impactX;
		int impactY;
		int SavedCombo[176];
		int SavedLoc[176];
		//Increase size of platform for larger enemies.
		int BlockWidth;
		int BlockHeight;
		int Ice_Timer;//Used internally by ffc to track how long ice combo has existed.
		int i;
		int j;
		int loc;
		int loc2;
		lweapon beam = FindMiscLWeapon(Weapon_ID);//Find previously saved lweapon.
		//Position ffc.
		if(beam->isValid()){
			this->X = beam->X;
			this->Y = beam->Y;
		}
		else{
			this->Data = 0;
			Quit();
		}
		while(!Stop){
			while(!impact && this->X>0 && this->Y>0 && this->X<256 && this->Y<170){
				//Adjust the position of the ffc every frame, based on the direction Link was facing when he fired it.
				if(beam->isValid()){
					this->X = beam->X;
					this->Y = beam->Y;
				}
				else{
					this->Data = 0;
					Quit();
				}
				//Update current number of enemies.
				for(i=Screen->NumNPCs();i>0;i--){
					current_enemy = Screen->LoadNPC(i);
					if(current_enemy->HP<=0)break;
					if (Collision(beam,current_enemy)){
						if(current_enemy->Defense[NPCD_SCRIPT]!=NPCDT_IGNORE
							&& (current_enemy->Misc[GEN_MISC_FLAGS]&NPC_F_FREEZE)==0
							&& current_enemy->Defense[NPCD_SCRIPT]!=NPCDT_BLOCK){
							impactX = current_enemy->X;
							impactY = current_enemy->Y;
							if(current_enemy->TileWidth>1)BlockWidth = current_enemy->TileWidth;
							if(current_enemy->TileHeight>1)BlockHeight = current_enemy->TileHeight;
							//Stun the enemy.
							current_enemy->Stun = MAX_ICE_PLATFORM_TIME;
							current_enemy->Misc[GEN_MISC_FLAGS]|=NPC_F_FREEZE;
							impact = true;
							this->Flags[FFCF_OVERLAY]= true;
							this->Flags[FFCF_TRANS] = true;
							if(beam->isValid() && !GetLWeaponFlag(beam,LWF_PIERCES_ENEMIES))
								KillLWeapon(beam);		
						}
						else{
							if(beam->isValid() && !GetLWeaponFlag(beam,LWF_PIERCES_ENEMIES))
								KillLWeapon(beam);
						}
					}
				}
				Waitframe();
			}
			//You've hit an enemy.
			if(impact){
				//Position the ffc.
				this->X = impactX;
				this->Y = impactY;
				//Change ffc appearance.
				loc = ComboAt(this->X,this->Y);
				if(BlockWidth>0){
					for(i=loc;i<(loc+BlockWidth);i++){
						if(!ComboFI(i,CF_NOENEMY) &&
							!ComboT(i,CT_NOENEMY)){
							SavedCombo[i]= Screen->ComboD[ComboAt(ComboX(i),ComboY(i))];
							//Set these combos so they're retained between screens.
							Game->SetComboData(Game->GetCurMap(),Game->GetCurDMapScreen(),i,ICE_BLOCK_COMBO);
						}
					}
				}
				else{
					if(!ComboFI(loc,CF_NOENEMY)
						&& !ComboT(loc,CT_NOENEMY))
						Game->SetComboData(Game->GetCurMap(),Game->GetCurDMapScreen(),loc,ICE_BLOCK_COMBO);
				}
				if(Screen->D[MISC_ICE_REGISTER]==0)
					Screen->D[MISC_ICE_REGISTER]= 600;
			}
			while(Screen->D[MISC_ICE_REGISTER]>0){
				if(Screen->D[MISC_ICE_REGISTER]<5){
					if(current_enemy->isValid())
						current_enemy->Misc[GEN_MISC_FLAGS]&=~NPC_F_FREEZE;
					if(BlockWidth>0){
						for(i=loc;i<(loc+BlockWidth);i++)
							Screen->ComboD[i]=SavedCombo[i];
							
					}
					Screen->D[MISC_ICE_REGISTER]=0;
					Stop = true;
					break;
				}
				else{
					Screen->DrawCombo(6, current_enemy->X,current_enemy->Y, current_enemy->Attributes[10],
										current_enemy->TileWidth,current_enemy->TileHeight,FROZEN_ENEMY_CSET, 
										-1, -1, 0, 0, 0, 0, 0, true, 128);
				}
				Waitframe();
			}
			if(Screen->D[MISC_ICE_REGISTER]<=0){
				if(BlockWidth>0){
					for(i=loc;i<(loc+BlockWidth);i++)
						Screen->ComboD[i]=SavedCombo[i];
				}
				if(current_enemy->isValid())
					current_enemy->Misc[GEN_MISC_FLAGS]&=~NPC_F_FREEZE;
				//SamusVars[ICE_SCRIPT_CHECK]=0;	
				break;
			}
			Waitframe();	
		}
		this->Data = 0;
		Quit();
	}
}

const int FROZEN_ENEMY_CSET = 4;
const int ICE_TEMP_SCRIPT  = 94;
const int MISC_ICE_REGISTER = 7;

//Remove unwanted ice combos.
void Ice_CleanUp(){
	//Remove ice combos.
	if(Screen->D[MISC_ICE_REGISTER]>0){
		Screen->D[MISC_ICE_REGISTER]--;
		if(CountFFCsRunning(ICE_TEMP_SCRIPT)==0 &&
			CountFFCsRunning(ICE_BEAM_SCRIPT)==0)
			NewFFCScript(ICE_TEMP_SCRIPT,0);
	}
	else
		Screen->D[MISC_ICE_REGISTER]=0;
}

//Temporary ice platforms.

ffc script Ice_Temp{
	void run(){
		this->Data= GH_INVISIBLE_COMBO;
		int i;
		int NumCombos;
		while(Screen->D[MISC_ICE_REGISTER]>0){
			for(i =0;i<=175;i++){
				if(Screen->ComboD[i]==ICE_BLOCK_COMBO){
					Screen->FastCombo(6, ComboX(i), ComboY(i), ICE_FAKE_COMBO, 2,128);
				}
			}
			Waitframe();
		}	
		Screen->D[MISC_ICE_REGISTER]=0;
		for(i =0;i<=175;i++){
			if(Screen->ComboD[i]==ICE_BLOCK_COMBO)
				NumCombos++;
		}
		for(i =0;i<=175;i++){
			if(NumCombos!=0){
				if(Screen->ComboD[i]==ICE_BLOCK_COMBO)
					Game->SetComboData(Game->GetCurMap(),Game->GetCurDMapScreen(),i,GH_INVISIBLE_COMBO);
			}
		}
	}
}

